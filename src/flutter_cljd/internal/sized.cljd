(ns flutter-cljd.internal.sized
  (:require
   ["package:flutter/material.dart" :as m]
   [flutter-cljd.types :as t]))

(defn sized*
  ([] (sized* {} nil))
  ([value]
   (if (instance? m/Widget value)
     (sized* {} value)
     (sized* value nil)))
  ([value child]
   (m/ConstrainedBox
    .constraints (t/box-constraints value)
    .child child))
  ([width height child]
   (sized* {:width width :height height} child)))

(defn fractionally-sized*
  ([value]
   (if (instance? m/Widget value)
     (fractionally-sized* {} value)
     (fractionally-sized* value nil)))
  ([value child]
   (let [args (if (map? value) value {:w value :h value})]
     (m/FractionallySizedBox
      .alignment (t/alignment (:alignment args :center))
      .widthFactor (some-> (some args [:w :width]) double)
      .heightFactor (some-> (some args [:h :height]) double)
      .child child)))
  ([width height child]
   (fractionally-sized* {:width width :height height} child)))
